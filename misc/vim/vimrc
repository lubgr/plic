
function! s:insertIncludeGuard()
    let fullname = toupper(expand('%'))
    let reducedname = substitute(fullname, 'src\/', '', '')
    let gatename = substitute(substitute(reducedname, '\.', '_', 'g'), '\/', '_', 'g')
    execute "normal! i#ifndef PLIC_" . gatename
    execute "normal! o#define PLIC_" . gatename . "\n"
    execute "normal! Go#endif"
    normal! kk
endfunction

autocmd BufNewFile *.{h,hpp} call <SID>insertIncludeGuard()

map <F10> :argadd `git ls-files '*.h' '*.cpp'`

call add(g:UltiSnipsSnippetDirectories, getcwd() . "/.ultisnips")

set path=bin/**,misc/**,src/**,test/**

set shellpipe=2>&1\ \|
            \while\ read\ line;
            \do\ mod=`echo\ $line\ \|
            \sed\ -e\ 's/build.src/src/g'\ -e\ 's/build.test/test/g'`;
            \echo\ $mod\ >>\ %s;
            \echo\ $line;
            \done

func! AlternateImplVSplit()
    let currentFile = expand('%:t')

    let baseName = matchstr(currentFile, '.*\.')
    let header = matchstr(currentFile, '\.h')
    let impl =  matchstr(currentFile, '\.cpp')

    if (strlen(header))
        let suffix = 'cpp'
    elseif (strlen(impl))
        let suffix = 'h'
    else
        return
    endif

    let newFile = baseName . suffix

    vsplit
    execute "normal! \<C-W>\<C-W>"

    try
       execute 'find ' . newFile
    catch /E345/
       execute "normal! :q\<CR>"
    endtry
endfu

command! LocalAV call AlternateImplVSplit()
cabbrev av LocalAV

function! GetTestCaseAndGroup()
    let l:pattern = '^ *TEST *( *\([^ ]\+\) *, *\([^ ]\+\) *) *$'
    let l:nLine = line('.')

    while getline(l:nLine) !~ l:pattern
        let l:nLine = l:nLine - 1
        if l:nLine == 0
            return ['', '']
        endif
    endwhile

    let l:match = matchlist(getline(l:nLine), l:pattern)
    return l:match[1:2]
endfunction

function! RunTestGroupCursorIsIn()
    let l:group = GetTestCaseAndGroup()[0]
    let l:cmd = '!runtests -c -sg ' . l:group
    execute l:cmd
endfunction

function! RunTestCaseCursorIsIn()
    let l:group = GetTestCaseAndGroup()[0]
    let l:test = GetTestCaseAndGroup()[1]

    let l:cmd = '!runtests -c -sg ' . l:group . ' -sn ' . l:test
    execute l:cmd
endfunction

map <F11> :call RunTestCaseCursorIsIn()<CR>
map <F12> :call RunTestGroupCursorIsIn()<CR>
